//-------------------------------------------------
// DOM elements
//-------------------------------------------------

let ~selectedFilter =
  let element = first(`.todoapp .filters a.selected`);
  if element != null && element.visible { element.textContent };

let ~items =
  let listItems = `.todo-list li`;
  for [index, [itemVisible, isEditing], [text, labelVisible], checked] in zipAll([
    range(0, length(listItems) - 1),
    (for item in listItems { [item.css.display != "none", contains("editing", item.classList)] }),
    (for label in `.todo-list li label` { [label.textContent, label.visible] }),
    (for cb in `.todo-list li input[type=checkbox]` { cb.checked })
  ]) {
    if itemVisible && (labelVisible || isEditing) {
      { index: index, text: text, checked: checked, isEditing: isEditing }
    }
  };

let ~editInput = first(for e in `.todo-list li .edit` { { text: e.textContent, active: e.active } when e.visible });

let ~lastItemText =
  let item = last(items);
  if item != null { item.text };

let ~numItems = length(items);

let ~numUnchecked = length(for i in items { i when not i.checked });

let ~numChecked = length(for i in items { i when i.checked });

let ~itemsInEditMode = for item in items { item when item.isEditing };

let ~itemInEditMode = first(itemsInEditMode);

let ~numInEditMode = length(itemsInEditMode);

let ~isInEditMode = numInEditMode >= 1 && editInput != null;

let ~newTodoInput =
  first(for e in `.todoapp .new-todo` {
    { pendingText: e.value, active: e.active }
  });

let ~todoCount =
    let strong = first(for e in `.todoapp .todo-count strong` { e.textContent when e.visible });
    let t = first(split(" ", trim(strong))) when strong != null;
    if t != null && t != "" { parseInt(t) };

let ~itemsLeft = first(for e in `.todoapp .todo-count` { e.innerText when e.visible });

let ~availableFilters = for f in `.todoapp .filters a` { f.textContent };

let ~toggleAllLabel =
  let t = first(`.todoapp label[for=toggle-all]`);
  t when t != null && t.visible;

let ~toggleAllChecked = exists c in `.todoapp #toggle-all` { c.checked };
